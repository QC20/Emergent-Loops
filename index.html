<!DOCTYPE html>
<html lang="en">
<head>
  <meta charset="UTF-8">
  <meta name="viewport" content="width=device-width, initial-scale=1.0">
  <title>Emergent Loops</title>
  <link rel="stylesheet" href="./src/styles/style.css">
</head>
<body>
<script type="x-shader/vertex" id="vertex">#version 300 es
in vec3 position;
in vec2 uv;

out vec2 v_uv;

void main() {
  gl_Position = vec4(position, 1.0);
  v_uv = uv;
}
</script>
<script type="x-shader/fragment" id="fragment">#version 300 es
precision highp float;
  
uniform vec2 u_resolution;
uniform float u_time;
uniform float u_frame;
  
in vec2 v_uv;
  
out vec4 colour;
  
//	Simplex 3D Noise 
//	by Ian McEwan, Stefan Gustavson (https://github.com/stegu/webgl-noise)
//
vec4 permute(vec4 x){return mod(((x*34.0)+1.0)*x, 289.0);}
vec4 taylorInvSqrt(vec4 r){return 1.79284291400159 - 0.85373472095314 * r;}

float snoise(vec3 v){ 
  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

// First corner
  vec3 i  = floor(v + dot(v, C.yyy) );
  vec3 x0 =   v - i + dot(i, C.xxx) ;

// Other corners
  vec3 g = step(x0.yzx, x0.xyz);
  vec3 l = 1.0 - g;
  vec3 i1 = min( g.xyz, l.zxy );
  vec3 i2 = max( g.xyz, l.zxy );

  //  x0 = x0 - 0. + 0.0 * C 
  vec3 x1 = x0 - i1 + 1.0 * C.xxx;
  vec3 x2 = x0 - i2 + 2.0 * C.xxx;
  vec3 x3 = x0 - 1. + 3.0 * C.xxx;

// Permutations
  i = mod(i, 289.0 ); 
  vec4 p = permute( permute( permute( 
             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
           + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

// Gradients
// ( N*N points uniformly over a square, mapped onto an octahedron.)
  float n_ = 1.0/7.0; // N=7
  vec3  ns = n_ * D.wyz - D.xzx;

  vec4 j = p - 49.0 * floor(p * ns.z *ns.z);  //  mod(p,N*N)

  vec4 x_ = floor(j * ns.z);
  vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

  vec4 x = x_ *ns.x + ns.yyyy;
  vec4 y = y_ *ns.x + ns.yyyy;
  vec4 h = 1.0 - abs(x) - abs(y);

  vec4 b0 = vec4( x.xy, y.xy );
  vec4 b1 = vec4( x.zw, y.zw );

  vec4 s0 = floor(b0)*2.0 + 1.0;
  vec4 s1 = floor(b1)*2.0 + 1.0;
  vec4 sh = -step(h, vec4(0.0));

  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

  vec3 p0 = vec3(a0.xy,h.x);
  vec3 p1 = vec3(a0.zw,h.y);
  vec3 p2 = vec3(a1.xy,h.z);
  vec3 p3 = vec3(a1.zw,h.w);

//Normalise gradients
  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
  p0 *= norm.x;
  p1 *= norm.y;
  p2 *= norm.z;
  p3 *= norm.w;

// Mix final noise value
  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
  m = m * m;
  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
                                dot(p2,x2), dot(p3,x3) ) );
}
vec2 getScreenSpace() {
  vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution.xy) / min(u_resolution.y, u_resolution.x);

  return uv;
}
void main() {
  vec2 uv = getScreenSpace();
  
  vec2 euv = uv;
  uv = vec2(length(uv), atan(uv.y, uv.x));
  
  uv.x += 900.0 - u_time * 0.5; 

  // Setup the grid and noise scale
  float cells = 6.0;
  float stipes = 50.0;
  float x_scaled = uv.x * cells;
  
  float id = floor(x_scaled);
  float local_x = fract(x_scaled);
  float blend = smoothstep(0.80, 1.0, local_x);
  float smooth_id = (id + blend) * 0.16;
  
  float noise_z = u_time * 0.5 + smooth_id;

  // To loop the noise around the angle (uv.y), we map the angle to a circle (sin/cos).
  vec2 loop1 = vec2(cos(uv.y), sin(uv.y)) * .5; 
  float z1 = (uv.x * 2.0) + noise_z; 
  vec2 loop2 = vec2(cos(euv.y), sin(euv.y)) * 0.5;
  float z2 = (uv.x * 0.5 + 1.0) + noise_z;
  // Mixing polar and cartesian noise for something interesting
  float n = snoise(vec3(loop2, z2)) * 0.5 
           + snoise(vec3(loop1, z1));
  
  float ng = n * stipes;
  float aa = fwidth(sin(ng));

  colour.r = smoothstep(0., aa, sin(ng));
  colour.g = smoothstep(0., aa, sin(ng + 0.1));
  colour.b = smoothstep(0., aa, sin(ng + 0.2));
  colour.a = 1.0;
}
</script>
<script type="module" src="./src/javascript/sketch.js"></script>
</body>
</html>